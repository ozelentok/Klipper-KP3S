pkgname=klipper-tmc-autotune-git
_pkgname=klipper_tmc_autotune
pkgver=r231.6224b77
pkgrel=1
pkgdesc="Klipper extension for automatic configuration and tuning of TMC drivers"
arch=('any')
url="https://github.com/andrewmcgr/${_pkgname}"
license=('GPL3')
depends=('klipper')
makedepends=('python')
source=("git+https://github.com/andrewmcgr/${_pkgname}")
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir/$_pkgname"
  python -m compileall .
}

package() {
  cd "$srcdir/$_pkgname"

  install -Dm644 "autotune_tmc.py"    "$pkgdir/usr/lib/klipper/klippy/extras/autotune_tmc.py"
  install -Dm644 "motor_constants.py" "$pkgdir/usr/lib/klipper/klippy/extras/motor_constants.py"
  install -Dm644 "motor_database.cfg" "$pkgdir/usr/lib/klipper/klippy/extras/motor_database.cfg"
  cp -r          "__pycache__"        "$pkgdir/usr/lib/klipper/klippy/extras"
}
