pkgname=moonraker-oz
_pkgname=moonraker
pkgver=20240803
pkgrel=1
pkgdesc="HTTP frontend for Klipper 3D printer firmware"
arch=('any')
url="https://github.com/Arksine/moonraker"
license=('GPL3')
depends=(
  klipper
  git
  curl
  polkit
  python-tornado
  python-pyserial
  python-pillow
  python-distro
  python-zeroconf
  python-paho-mqtt
  python-jinja
  python-dbus-next
  apprise
  python-ldap3
  python-uvloop
  libsodium
)
optdepends=(
  'wireless_tools: network detection'
)
provides=("$_pkgname")
conflicts=("$_pkgname")
backup=("var/lib/klipper/${_pkgname}.conf")
install="${_pkgname}.install"
source=(
  'moonraker.conf'
  'moonraker.service'
  'moonraker.rules'
  'sysusers.conf'
  'tmpfiles.conf'
)
sha256sums=(
  '74d540ce4a5a2186833adde0aba0f8d9a69922cde10de7c47b39c684d9c86bef'
  'f1a3e4df12a7a99da0479c840dadb7cfd8455d4c3009c3a7e90e64b6d2f93f96'
  '9fa5608f9af813841c1b47e730a1ae6b04eae4623c5260b70cda30ad2f83adfc'
  '549309fd129c8c665a5aed2d4229c20e5a9927f4fbdc937e0982db4785b9ee0d'
  '7dc70e62589d85d97fac598dce73f3ce052c76a82a77dd8a10c36ee7ad5a4ff5'
)

package() {
  install -Dm644 "$srcdir/moonraker.service" "$pkgdir/usr/lib/systemd/system/${_pkgname}.service"
  install -Dm644 "$srcdir/sysusers.conf"     "$pkgdir/usr/lib/sysusers.d/${_pkgname}.conf"
  install -Dm644 "$srcdir/tmpfiles.conf"     "$pkgdir/usr/lib/tmpfiles.d/${_pkgname}.conf"
  install -Dm644 "$srcdir/${_pkgname}.conf"  "$pkgdir/var/lib/klipper/${_pkgname}.conf"

  # match directory owner/group and mode from [extra]/polkit
  install -d -o root -g root -m 755          "$pkgdir/usr/share/polkit-1/rules.d"
  install -Dm644 "$srcdir/moonraker.rules"   "$pkgdir/usr/share/polkit-1/rules.d/moonraker.rules"
}
