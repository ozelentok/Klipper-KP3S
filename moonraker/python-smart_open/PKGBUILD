pkgname=python-smart_open-git
_pkgname=smart_open
pkgver=7.0.4
pkgrel=1
pkgdesc="Library for efficient streaming of very large files from/to S3, HDFS, WebHDFS, HTTP, or local (compressed) files"
arch=('any')
url="https://github.com/RaRe-Technologies/smart_open"
license=('MIT')
depends=("python-wrapt")
optdepends=("python-boto3: AWS support"
  "python-requests: HTTP support"
  "python-paramiko: SSH support"
  "python-zstandard: zstd support")
makedepends=('python-build' 'python-installer' 'python-wheel' 'python-setuptools')
provides=('python-smart_open')
conflicts=('python-smart_open')
source=("git+https://github.com/piskvorky/smart_open.git#branch=master")
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir/$_pkgname"
  rm -f dist/*.whl
  python -m build --wheel --no-isolation
}

package() {
  cd "$srcdir/$_pkgname"
  python -m installer --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
