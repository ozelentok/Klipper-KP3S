pkgname=python-pywavelets-git
_pkgname=python-pywavelets
pkgver=r1965.68ed81d
pkgrel=1
pkgdesc="Wavelet transform module"
arch=('x86_64' 'aarch64')
url="https://github.com/PyWavelets/pywt"
license=('MIT')
depends=(python-numpy)
makedepends=(
  git
  python-build
  python-installer
  meson-python
  python-setuptools
  python-wheel
  cython
)
provides=("$_pkgname")
conflicts=("$_pkgname")
source=("${_pkgname}::git+https://github.com/PyWavelets/pywt")
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir/$_pkgname"
  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "$srcdir/$_pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
