pkgname=klipper-shaketune-git
_pkgname=klipper-shaketune
pkgver=r77.d0b0cc0
pkgrel=1
pkgdesc="Klipper extension for calibrating and tuning the input shaper system"
arch=('any')
url="https://github.com/ozelentok/${_pkgname}"
license=('GPL3')
depends=('klipper'
         'python-matplotlib'
         'python-numpy'
         'python-scipy')
makedepends=('python')
source=("git+https://github.com/ozelentok/${_pkgname}")
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir/$_pkgname"
  python -m compileall .
}

package() {
  cd "$srcdir/$_pkgname"

  install -Dm755 -d "$pkgdir/usr/lib/klipper/klippy/extras"
  cp -r "shaketune" "$pkgdir/usr/lib/klipper/klippy/extras/shaketune"

  install -Dm755 -d "$pkgdir/var/lib/klipper/config"
  cp -r "macros"    "$pkgdir/var/lib/klipper/config/shaketune"

  install -Dm755 "scripts/shaketune_process.py" "$pkgdir/usr/lib/klipper/scripts/shaketune_process.py"
}
